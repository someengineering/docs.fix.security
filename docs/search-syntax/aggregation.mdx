---
sidebar_position: 5
sidebar_label: Aggregation
draft: true
---

# Search aggregation

There are several situations where specific data is not too relevant but needs lifting to a higher level. That is where aggregation comes into play. Aggregation allows grouping entities by one or more properties and then do math operations on that group.

For example, the following search will select and count compute instances that are older than 3 years:

```bash
is(instance) and age > 3y | count
```

## Aggregation functions

Aggregations can utilize the following functions:

- `sum`
- `min`
- `max`
- `avg`

Function arguments can be variable names (e.g., `min(path.to.prop)`), static values (e.g., `sum(1)`), or even calculations using simple expressions (`min(path.to.prop * 3 + 2)`).

Each grouping function can have an `as <name>` clause to give the function result a specific name: `<function>(..) as <name>`. If this `as <name>` clause is omitted, a name is derived from the function name and property path.

Example: `min(memory)`, `sum(1) as count`, `avg(instance_cores) as average_cores`.

Fix Security aggregates search results based on the defined criteria. Each result is then passed to the defined aggregation function(s).

The above example using `count` could also be rewritten with `aggregate` like so:

```bash
is(instance) and age > 3y |
  aggregate sum(1) as count
```

Every element is counted as `1`, so `sum(1)` is the number of elements.

It is also possible to define multiple aggregation functions. Let's count both instances and cores:

```bash
is(instance) and age > 3y |
  aggregate
    sum(1) as count,
    sum(instance_cores) as cores
```

We could even compute the average, minimum, and maximum number of available cores:

```bash
is(instance) and age > 3y |
  aggregate
    sum(1) as count,
    sum(instance_cores) as cores,
    min(instance_cores) as min_cores,
    max(instance_cores) as max_cores,
    avg(instance_cores) as avg_cores
```

## Aggregation groups

The real power of aggregations is in defining groups and applying functions on those groups.

A group is defined using a property path. The value of this path is looked up in every document. All documents with the same value form a group.

For example, instances can be grouped by status:

```bash
is(instance) and age > 3y |
  aggregate instance_status: sum(1) as count
```

Grouping variable can be named using `as`. By default, the last part of the path is used as the variable name.

Additional aggregation functions also get applied on each group:

```bash
is(instance) and age > 3y |
  aggregate instance_status as status:
    sum(1) as count,
    sum(instance_cores) as cores,
    min(instance_cores) as min_cores,
    max(instance_cores) as max_cores,
    avg(instance_cores) as avg_cores
```

Groups can also be defined using multiple grouping variables:

```bash
is(instance) and age > 3y |
  aggregate
    instance_status as status, instance_type as type:
      sum(1) as count,
      sum(instance_cores) as cores
```
